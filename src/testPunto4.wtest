import main.*
import artefactos.*
import global.*
import refuerzos.*
import hechizos.*
import comercio.*

describe "Rolando tiene espada del destino, el collar divino, la mascara oscura, una armadura sin refuerzo y un espejo como artefactos" {

	const hoy = new Date()
	var alacachulacachicomula = new Logo("alacachula cachicomula", 1)
	var xenia = new Jugador(alacachulacachicomula, 1, [], 200)
	var thor = new Jugador(hechizoBasico, 1, [], 200)
	var loki = new Jugador(hechizoBasico, 1, [], 200)
	var espada = new Arma(0, hoy)
	var cota = new CotaDeMalla(5)
	var armaduraSinRefuerzo = new Armadura(0, 20, ninguno)
	var armaduraCota = new Armadura(0, 2, cota)
	var armaduraBendecida = new Armadura(0, 2, bendicion)
	var armaduraHechizada = new Armadura(0, 2, hechizo)
	var mockHnos = new Comerciante(independiente, 0)

	fixture {
		loki.oro(5)
		libroDeHechizos.hechizos([ alacachulacachicomula, hechizoBasico ])
		hechizo.hechizoRefuerzo(alacachulacachicomula)
	}

	test "Precio de lista de alacachula cachicomula" {
		assert.equals(22, alacachulacachicomula.precioDeVentaSegunComerciante(mockHnos))
	}

	test "Xenia cambia su hechizo por un hechizo basico" {
		xenia.comprarHechizo(hechizoBasico, mockHnos)
		assert.equals(100, xenia.oro())
	}

	test "Thor compra alacachula cachicomula" {
		thor.comprarHechizo(alacachulacachicomula, mockHnos)
		assert.equals(83, thor.oro())
	}

	test "Thor compra 2 veces hechizo basico" {
		thor.comprarHechizo(hechizoBasico, mockHnos)
		thor.comprarHechizo(hechizoBasico, mockHnos)
		assert.equals(90, thor.oro())
	}

	test "Loki intenta comprar alacachulacachicomula" {
		assert.throwsExceptionWithMessage("No hay oro suficiente para pagar", { => loki.comprarHechizo(alacachulacachicomula, mockHnos)})
	}

	test "Xenia compra una espada, un collar divino, una armadura sin refuerzo y una armadura con malla de cota " {
		xenia.comprarArtefacto(espada, mockHnos)
		xenia.comprarArtefacto(collarDivino, mockHnos)
		xenia.comprarArtefacto(armaduraSinRefuerzo, mockHnos)
		assert.equals(88, xenia.oro())
	// xenia.comprarArtefacto(armaduraCota, mockHnos)
	// assert.equals(85.5,xenia.oro())
	}

	test "Xenia compra 4 artefactos" {
		xenia.comprarArtefacto(espada, mockHnos)
		xenia.comprarArtefacto(collarDivino, mockHnos)
		xenia.comprarArtefacto(armaduraSinRefuerzo, mockHnos)
		xenia.comprarArtefacto(armaduraCota, mockHnos)
		assert.equals(4, xenia.artefactos().size())
	}

	test "Thor compra una armadura con bendicion y una armadura con el hechizo alacula" {
		thor.comprarArtefacto(armaduraBendecida, mockHnos)
		thor.comprarArtefacto(armaduraHechizada, mockHnos)
		assert.equals(74, thor.oro())
	}

	test "Loki quiere comprar el espejo" {
		assert.throwsExceptionWithMessage("No hay oro suficiente para pagar", { => loki.comprarArtefacto(espejo, mockHnos)})
	}

	test "Xenia quiere comprar libro de hechizos" {
		libroDeHechizos.hechizos([ alacachulacachicomula, hechizoBasico ])
		xenia.comprarArtefacto(libroDeHechizos, mockHnos)
		assert.equals(58, xenia.oro())
	}

}

